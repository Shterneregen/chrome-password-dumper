plugins {
    id "java"
    id "application"
    id "org.beryx.jlink" version "$beryxJlinkVersion"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation(
            "org.xerial:sqlite-jdbc:$sqliteJdbcVersion",
            "net.java.dev.jna:jna-platform:$jnaPlatformVersion",
            "org.json:json:$jsonVersion",
            "info.picocli:picocli:$picocliVersion"
    )
    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = "random.Main"
}

jlink {
    launcher {
        name = "dumper"
    }

    // Install Wix Toolset before using jpackage. And add it to the PATH variable
    // https://wixtoolset.org/docs/wix3/
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        imageOptions += ["--win-console"]
        if (currentOs.windows) {
            installerOptions += ["--win-per-user-install", "--win-dir-chooser", "--win-menu"]
        }
    }
}

tasks.register("fatJar", Jar) {
    group "build"
    manifest { attributes "Main-Class": "random.Main" }
    archiveFileName = "dumper.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
